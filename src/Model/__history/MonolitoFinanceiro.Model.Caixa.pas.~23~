unit MonolitoFinanceiro.Model.Caixa;

interface

uses
  System.SysUtils, System.Classes, FireDAC.Stan.Intf, FireDAC.Stan.Option,
  FireDAC.Stan.Param, FireDAC.Stan.Error, FireDAC.DatS, FireDAC.Phys.Intf,
  FireDAC.DApt.Intf, FireDAC.Stan.Async, FireDAC.DApt, Data.DB,
  Datasnap.DBClient, Datasnap.Provider, FireDAC.Comp.DataSet, MonolitoFinanceiro.Model.Conexao,
  FireDAC.Comp.Client, MonolitoFinanceiro.Entidades.Caixa.Resumo,
  MonolitoFinanceiro.Entidades.Caixa.Lancamento;

type
  TdmCaixa = class(TDataModule)
    sqlCaixa: TFDQuery;
    dspCaixa: TDataSetProvider;
    cdsCaixa: TClientDataSet;
    cdsCaixaID: TStringField;
    cdsCaixaNUMERO_DOCUMENTO: TStringField;
    cdsCaixaDESCRICAO: TStringField;
    cdsCaixaVALOR: TFMTBCDField;
    cdsCaixaTIPO: TStringField;
    cdsCaixaDATA_CADASTRO: TDateField;
  private
    { Private declarations }
    function GetSaldoAnteriorCaixa(Data : TDate) : Currency;
    function GetTotalEntradasCaixa(DataInicial, DataFinal : TDate) : Currency;
    function GetTotalSaidasCaixa(DataInicial, DataFinal : TDate) : Currency;
  public
    { Public declarations }
    function ResumoCaixa(DataInicial, DataFinal : TDate) : TModelResumoCaixa;
    procedure GravarLancamentoCaixa(aValue: TModelCaixaLancamento; SQLGravar: TFDQuery);
  end;

var
  dmCaixa: TdmCaixa;

implementation

{$R *.dfm}

{ TdmCaixa }

function TdmCaixa.GetSaldoAnteriorCaixa(Data: TDate): Currency;
var
  SQLConsulta : TFDQuery;
  TotalEntradas : Currency;
  TotalSaidas : Currency;
begin
  Result := 0;
  SQLConsulta := TFDQuery.Create(nil);
  try
    SQLConsulta.Connection := dmConexao.SQLConexao;
    SQLConsulta.SQL.Clear;
    SQLConsulta.SQL.Add('SELECT IFNULL (SUM(VALOR), 0) AS VALOR FROM CAIXA WHERE TIPO = ''R''');
    SQLConsulta.SQL.Add(' AND DATA_CADASTRO < :DATA');
    SQLConsulta.Parambyname('DATA').AsDate := Data;
    SQLConsulta.Open;
    TotalEntradas := SQLConsulta.fieldbyname('VALOR').AsCurrency;

    SQLConsulta.Connection := dmConexao.SQLConexao;
    SQLConsulta.SQL.Clear;
    SQLConsulta.SQL.Add('SELECT IFNULL (SUM(VALOR), 0) AS VALOR FROM CAIXA WHERE TIPO = ''D''');
    SQLConsulta.SQL.Add(' AND DATA_CADASTRO < :DATA');
    SQLConsulta.Parambyname('DATA').AsDate := Data;
    SQLConsulta.Open;
    TotalSaidas := SQLConsulta.fieldbyname('VALOR').AsCurrency;
  finally
    SQLConsulta.Free;
  end;
  Result := TotalEntradas - TotalSaidas;
end;

function TdmCaixa.GetTotalEntradasCaixa(DataInicial,
  DataFinal: TDate): Currency;
var
  SQLConsulta : TFDQuery;
begin
  Result := 0;
  SQLConsulta := TFDQuery.Create(nil);
  try
    SQLConsulta.Connection := dmConexao.SQLConexao;
    SQLConsulta.SQL.Clear;
    SQLConsulta.SQL.Add('SELECT IFNULL (SUM(VALOR), 0) AS VALOR FROM CAIXA WHERE TIPO = ''R''');
    SQLConsulta.SQL.Add(' AND DATA_CADASTRO BETWEEN :DATAINICIAL AND :DATAFINAL');
    SQLConsulta.Parambyname('DATAINICIAL').AsDate := DataInicial;
    SQLConsulta.Parambyname('DATAFINAL').AsDate := DataFinal;
    SQLConsulta.Open;
    Result := SQLConsulta.FieldByName('VALOR').AsCurrency;
  finally
    SQLConsulta.Free;
  end;

end;

function TdmCaixa.GetTotalSaidasCaixa(DataInicial, DataFinal: TDate): Currency;
var
  SQLConsulta : TFDQuery;
begin
  Result := 0;
  SQLConsulta := TFDQuery.Create(nil);
  try
    SQLConsulta.Connection := dmConexao.SQLConexao;
    SQLConsulta.SQL.Clear;
    SQLConsulta.SQL.Add('SELECT IFNULL (SUM(VALOR), 0) AS VALOR FROM CAIXA WHERE TIPO = ''D''');
    SQLConsulta.SQL.Add(' AND DATA_CADASTRO BETWEEN :DATAINICIAL AND :DATAFINAL');
    SQLConsulta.Parambyname('DATAINICIAL').AsDate := DataInicial;
    SQLConsulta.Parambyname('DATAFINAL').AsDate := DataFinal;
    SQLConsulta.Open;
    Result := SQLConsulta.FieldByName('VALOR').AsCurrency;
  finally
    SQLConsulta.Free;
  end;
end;

procedure TdmCaixa.GravarLancamentoCaixa(aValue: TModelCaixaLancamento;
  SQLGravar: TFDQuery);
var
  SQL: string;
begin
  SQL := 'INSERT INTO CAIXA (ID, NUMERO_DOCUMENTO, DESCRICAO, VALOR,' +
         ' VALOR, TIPO, DATA_CADASTRO) VALUES (:IDCAIXA, :NUMERO_DOC_CAIXA,' +
         ' :DESCRICAO_CAIXA, :VALOR_CAIXA, :TIPO, :DATA_CADASTRO)';

  SQLGravar.SQL.Clear;
  SQLGravar.Params.Clear;
  SQLGravar.SQL.Add(SQL);
  SQLGravar.ParamByName('IDCAIXA').AsString := aValue.ID;
  SQLGravar.ParamByName('NUMERO_DOC_CAIXA').AsString := aValue.NumeroDoc;
  SQLGravar.ParamByName('DESCRICAO_CAIXA').AsString := aValue.Descricao;
  SQLGravar.ParamByName('VALOR_CAIXA').AsCurrency := aValue.Valor;
  SQLGravar.ParamByName('TIPO').AsString := aValue.Tipo;
  SQLGravar.ParamByName('DATA_CADASTRO').AsDate := aValue.DataCadastro;
  SQLGravar.Prepare;
  SQLGravar.ExecSQL;
end;

function TdmCaixa.ResumoCaixa(DataInicial, DataFinal: TDate): TModelResumoCaixa;
begin
  if DataInicial > DataFinal then
    raise Exception.Create('Período Inválido');

  Result := TModelResumoCaixa.Create;
  try
    Result.SaldoInicial := GetSaldoAnteriorCaixa(DataInicial);
    Result.TotalEntradas := GetTotalEntradasCaixa(DataInicial, DataFinal);
    Result.TotalSaidas := GetTotalSaidasCaixa(DataInicial, DataFinal);
  except
    Result.Free;
    raise;
  end;
end;

end.
